# Shell typing convenience.

init_env() {
  INFOPATH=$INFOPATH:/opt/local/share/info
  MANPATH=$MANPATH:/opt/local/share/man
  PATH="$PATH:/opt/local/bin:/opt/local/sbin:$HOME/bin:$HOME/bin/shared"
}

init_rc() {
  alias e=subl
  alias la='ls -lAh'
  alias ll='ls -hl'
  alias ls="ls -FG"
  alias new-uuid='uuidgen | tr "[:upper:]" "[:lower:]"'

  # Use `lf` for directory navigation. Taken from: https://gist.github.com/LukeSmithxyz/e62f26e55ea8b0ed41a65912fbebbe52
  lfcd() {
    local tmp="$(mktemp)"
    lf -last-dir-path="$tmp" "$@"
    if [[ -f "$tmp" ]]; then
      local dir="$(cat "$tmp")"
      rm -rf "$tmp"
      [[ -d "$dir" ]] && [[ "$dir" != "$(pwd)" ]] && cd "$dir"
    fi
  }
}

init_rc_zsh() {
  KEYTIMEOUT=1 # To reduce delay when switching to Vi mode.

  bindkey '^I' complete-word
  bindkey "^[[3~" delete-char
  bindkey -v # ESC: Vi mode

  setopt correct # Suggest correction on typos.

  # Known hosts auto completion for ssh and similar commands:
  zstyle -e ':completion:*:(ssh|scp|sftp|rsh|rsync):hosts' hosts 'reply=(${=${${(f)"$(cat {/etc/ssh_,~/.ssh/known_}hosts(|2)(N) /dev/null)"}%%[# ]*}//,/ })'

  # Change cursor shape for different vi modes.
  # Taken from: https://gist.github.com/LukeSmithxyz/e62f26e55ea8b0ed41a65912fbebbe52#file-zshrc-L30
  function zle-keymap-select {
    if [[ ${KEYMAP} == vicmd ]] || [[ $1 = 'block' ]]; then
      echo -ne '\e[5 q'
    elif [[ ${KEYMAP} == main ]] || [[ ${KEYMAP} == viins ]] || [[ ${KEYMAP} = '' ]] || [[ $1 = 'beam' ]]; then
      echo -ne '\e[1 q'
    fi
  }
  zle -N zle-keymap-select
}

init_post_rc_zsh() {
  [[ -f /usr/local/bin/aws_zsh_completer.sh ]] && source /usr/local/bin/aws_zsh_completer.sh
  source "$HOME/.oh-my-zsh/custom/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
  bindkey -s '^o' 'lfcd\n' # CTRL+O: `lfcd`
  bindkey '^R' history-incremental-search-backward # CTRL+R: backwards history search
}
